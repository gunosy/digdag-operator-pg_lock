plugins {
    id 'scala'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group = 'com.github.Gunosy'
version = '0.0.3'

def digdagVersion = '0.10.5'
def scalaSemanticVersion = "2.13.12"
def depScalaVersion = "2.13"

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'io.digdag', name: 'digdag-spi', version: digdagVersion
    compile group: 'io.digdag', name: 'digdag-client', version: digdagVersion
    compile group: 'io.digdag', name: 'digdag-plugin-utils', version: digdagVersion

    // Follow digdag core dependencies. These are used only when compiling.
    // ref. https://github.com/treasure-data/digdag/tree/v0.10.5/digdag-core/build.gradle
    compile group: 'org.jdbi', name: 'jdbi', version: '2.75'
    compile group: 'com.zaxxer', name: 'HikariCP', version: '4.0.3'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.7.4'

    compile group: 'org.scala-lang', name: 'scala-library', version: scalaSemanticVersion
    compile group: 'org.flywaydb', name: 'flyway-core', version: '7.9.1'
    compile group: 'com.typesafe.scala-logging', name: "scala-logging_$depScalaVersion", version: '3.9.5'

    testCompile group: 'org.scalatest', name: "scalatest_$depScalaVersion", version: '3.2.17'
    testCompile group: 'org.scalatestplus', name: "junit-4-13_$depScalaVersion", version: '3.2.17.0'
    testCompile group: 'junit', name: 'junit', version: '4.13.2'
    testCompile group: 'io.digdag', name: 'digdag-cli', version: digdagVersion
}

shadowJar {
    classifier = null
    dependencies {
        exclude(dependency('io.digdag:.*'))
        exclude(dependency('com.fasterxml.jackson.*:.*:.*'))
        exclude(dependency('com.google.inject:.*:.*'))
        exclude(dependency('org.jdbi:jdbi:.*'))
        exclude(dependency('com.zaxxer:HikariCP:.*'))
        exclude(dependency('org.postgresql:postgresql:.*'))
        exclude(dependency('org.slf4j:slf4j-api:.*'))
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

test {
    jvmArgs '-Duser.language=en -Duser.country=US'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileScala.options.encoding = 'UTF-8'
compileTestScala.options.encoding = 'UTF-8'
compileScala.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

